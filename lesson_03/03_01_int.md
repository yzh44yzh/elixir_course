# Целые числа (Integer)

Все целые числа, независимо от знака и занимаемой памяти, относятся к одному типу -- **Integer**.

Диапазон значений не ограничен. Если значение укладывается в 1 машинное слово (4 байта на 32-х разрядной платформе, 8 байт на 64-х разрядной), то для него выделяется машинное слово. Если нужно больше памяти, то выделяется больше, и специальные биты обозначают, закончилось ли значение в текущем блоке памяти, или нужно читать следущий блок. Таким образом можно хранить любые значения, даже число атомов во вселенной.

Это удобно для программиста, потому что не нужно думать о переполнении. Но это не очень эффективно по производительности. Поэтому для Эликсир не характерны высокопроизводительные операции с числами.

Целые числа могут быть представлены разными способами. В разных системах исчисления: в десятичной, шеснадцатиричной, восьмеричной и двоичной:

```elixir
42
0x2A
0o52
0b101010
```

В экспоненциальном виде:

```elixir
0.42e2
```

Для больших чисел можно использовать символ подчеркивания между разрядами для удобства чтения:
```elixir
100_500
1_000_000
```

Для целых чисел поддерживается стандартный набор арифметических операций:
```elixir-iex
iex(1)> 20 + 22
42
iex(2)> 10 - 30
-20
iex(3)> 4 * 4
16
iex(4)> 100 / 15
6.666666666666667
```

целочисленное деление:
```elixir-iex
iex(5)> div(100, 15)
6
```

и взятие остатка:
```elixir-iex
iex(6)> rem(100, 15)
10
```

Функция `rem/2` заимствует знак первого аргумента для результата.
```elixir-iex
iex(13)> rem(12, 9)
3
iex(14)> rem(12, -9)
3
iex(15)> rem(-12, -9)
-3
iex(16)> rem(-12, 9)
-3
```

В разных языках программирования это поведение может отличаться. Для сравнения, в Python оператор `%` заимствует знак второго аргумента
```
>>> 12 % 9
3
>>> 12 % -9
-6
>>> -12 % 9
6
```

## Наибольший общий делитель

Для примера работы с целыми числами решим задачу вычисления наибольшего общего делителя (greatest common divisor).

Используем известный рекурсивный алгортим со взятием остатка от аргументов:
```elixir
defmodule IntExample do

  def gcd(a, b) do
    case rem(a, b) do
      0 -> b
      c -> gcd(b, c)
    end
  end

end
```

Важно продумать крайние случаи. Функция должна корректно работать с любыми аргументами. 

Что если один или оба аргумента -- отрицательные числа? Результат должен быть положительным. Обработаем эту ситуацию:
```elixir
  # ignore sign
  def gcd(a, b) when a < 0, do: gcd(-a, b)
  def gcd(a, b) when b < 0, do: gcd(a, -b)
```

Что, если один из агрументов `0`? Если это второй аргумент, то мы получим исключение:
```elixir-iex
iex(2)> rem(42, 0)
** (ArithmeticError) bad argument in arithmetic expression: rem(42, 0)
```

Если это первый аргумент, то результат будет `0`, и это правильно. Нужно обработать эту ситуацию:
```elixir
  # deal with 0
  def gcd(a, 0), do: a
```

Полная реализация:
```elixir
defmodule IntExample do
  # ignore sign
  def gcd(a, b) when a < 0, do: gcd(-a, b)
  def gcd(a, b) when b < 0, do: gcd(a, -b)

  # deal with 0
  def gcd(a, 0), do: a

  def gcd(a, b) do
    case rem(a, b) do
      0 -> b
      c -> gcd(b, c)
    end
  end
end
```

Добавим тесты, чтобы быть уверенными, что наш код работает правильно во всех ситуациях:
```elixir
ExUnit.start()

defmodule IntExample_Test do
  use ExUnit.Case
  import IntExample

  test "gcd" do
    assert gcd(12, 9) == 3
    assert gcd(9, 12) == 3
    assert gcd(60, 48) == 12
  end

  test "gcd with negative numbers" do
    assert gcd(24, 18) == 6
    assert gcd(24, -18) == 6
    assert gcd(-24, -18) == 6
    assert gcd(-24, 18) == 6
  end

  test "gcd with zero" do
    assert gcd(24, 0) == 24
    assert gcd(0, 18) == 18
    assert gcd(0, 0) == 0
  end
end
```

Запустим тесты:
```shell
elixir lib/int_example.exs
...
Finished in 0.3 seconds (0.3s on load, 0.00s async, 0.00s sync)
3 tests, 0 failures

Randomized with seed 691529
```

Как видим, наш код правильно обработывает и ту ситуацию, когда оба аргумента отрицательные:
```elixir
    assert gcd(-24, -18) == 6
```

В этом случае сперва выполняется `def gcd(a, b) when a < 0`, затем вызов идёт в `def gcd(a, b) when b < 0`, и только после этого вызов идёт в `def gcd(a, b) do`.
